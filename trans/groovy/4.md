# 四、数据流

在第 3 章中，我们使用了 findAll 和 collect 方法来过滤和转换数据。这些方法很方便，但请记住，它们会从每次调用中返回新的数据结构(如列表或地图)。当我们通过一系列方法(即管道)链接它们时，每个返回值使用的内存量(最终)会被清除，而下一个方法会获取另一个内存块。这一切都有点“矮胖”

当 [Java 8](https://docs.oracle.com/javase/tutorial/jdbc/basics/) 发布时，它包含了两个新特性——lambdas 和 streams。Groovy 开发人员已经有了闭包，所以 lambdas 并不是特别令人兴奋。事实上，Groovy 闭包可以简单地放在期望 lambdas 的地方，并且闭包语法更好(在我看来)。乍看之下，流似乎与 Groovy 添加到列表和映射中的许多方法扮演着相同的角色。然而，一些非常有用的差异使得流值得研究。

![](img/00014.jpeg)提示:对于较小的数据或简单的操作，我倾向于使用内置的 Groovy 方法，例如每个和 findAll，但是当它变得更复杂时，我会使用流。

让我们先看看我们是如何处理流的。首先，需要一个源，可以是有限的，也可以是无限的。对于我们一直在看的例子，来源可能是有限的项目列表，但是，因为我们在看天气记录，数据也可能是无限的。为了简单起见，我们将从以前使用的天气数据 CSV 文件中读入有限数量的数据。

流将让我们以一种看起来很像 SQL 的方式来分析数据。我们将能够过滤数据，执行聚合(例如，求和和求平均值)，将数据映射到不同的结构，以及分组和排序。这都是通过使用一系列方法调用来执行的，这些方法调用遵循以下过程:

1.创建流。

2.执行零个或多个中间操作。

3.执行一个终端操作。

这个过程中的元素被称为管道，很容易看出原因。想象数据开始于一个池塘或湖泊。然后，数据流经多个操作，并在最终输出中终止。我想象像尼罗河或亚马逊河这样的东西从一个源头开始，流入不同的溪流，最后到达一个河口。(我不是过度诗意，所以找一个最适合你的溪流和管道的类比！)

我们的天气数据在从 CSV 中读取后保存在一个列表中，这意味着创建一个流很容易——您只需在列表中调用 stream()方法。

中间操作，如过滤和映射，返回一个流，并允许进一步的中间操作。将这些操作想象为在原始数据上应用一个镜头，后续操作将通过这个镜头看到数据。然而，这些操作不同于内置的 Groovy 方法，例如 findAll，因为中间操作不返回新的数据值(例如，一个映射列表)。

中间操作被认为是懒惰的，因为它们实际上直到流到达终端操作时才被处理。终端操作标志着流的结束，通常用于通过某种计算减少数据或以某种方式收集数据。一旦调用了终端操作，您将无法再次访问该流，并且需要创建一个新的 stream。

原始数据源(例如，列表)不会被流改变，这是一个理想的特性。虽然一些流方法提供了修改底层数据的能力，但不建议这样做——这会降低您实现其他效率的能力。

当我们开始探索流时，我将通过对数据提出问题、描述回答问题的方法和提供输出来描述下面的例子。我将在这些示例中逐步使用流，这样我们就可以在每个阶段专注于特定的操作。

第一个问题是“2008 年 2 月有哪些记录？”代码清单 51:过滤一个流在阅读 CSV 文件时会遇到一些熟悉的问题，所以代码清单的后半部分是我们感兴趣的。为了创建一个流，我们简单地调用 weatherData.stream()，然后我们可以提供管道的其余部分作为方法调用链。

为了提取所需的记录，让我们使用过滤方法两次——一次是一年，另一次是一个月。过滤可以在一次调用中执行，但是我发现对于一组执行布尔“与”运算的过滤器(其中所有元素都必须为真)，将每个过滤器项目分解成它自己的调用更具可读性。我建议使用单个过滤器调用来执行布尔或。向 filter 方法传递一个闭包，该闭包定义了一个条目必须匹配才能满足筛选器的谓词。这与我们在 findAll 方法中看到的非常相似。

流管道的终端操作是收集()。这个调用导致过滤器方法被处理，结果子集被合并。然后，mappedData 变量保存与过滤器谓词匹配的(CSVRecord)项的列表。如果不调用 collect，mappedData 变量将保存一个等待其终端操作的流。

代码清单 52:过滤流的部分输出显示了来自 mappedData 变量的前几个 JSON 格式的列表项。这些是由 JsonOutput 库作为列表生成的，但是它们也可以在第 3 章解决方案基础一节中看到的方法中轻松处理。我将在下一个示例中改进输出。

```groovy
  import static groovy.json.JsonOutput.prettyPrint
  import
  static groovy.json.JsonOutput.toJson
  import
  static java.nio.file.Paths.get as getFile

  @Grab('org.apache.commons:commons-csv:1.2')
  import
  static org.apache.commons.csv.CSVFormat.*

  def
  weatherData
  = RFC4180.withHeader()
      .parse(getFile('../data/weather_data.csv').newReader())
      .getRecords()

  def
  mappedData
  = weatherData.stream()
      .filter { it.Year == '2008' }
      .filter { it.Month == '02' }
      .collect()

  print prettyPrint(toJson(mappedData))

```

```groovy
  [

  [

  "AU_QLD_098",

  "2008",

  "02",

  "01",

  "20",

  "5",

  "345.4"

  ],

  [

  "AU_QLD_098",

  "2008",

  "02",

  "02",

  "18",

  "8",

  "206.0"

  ],

```

下一个问题是“2008 年 2 月的日降雨量是多少？”正如您在代码清单 53:映射流中看到的，我创建了流，并以与前面清单相同的方式执行过滤。然后我调用映射中间操作，并向它传递闭包来执行映射。对于满足过滤器谓词的流中的每个项目，都会调用这个闭包。在闭包中，我设置了一个带有两个键的地图:

*   日期保存使用 YYY-MM-DD 的 [ISO 8601 格式](https://en.wikipedia.org/wiki/CamelCase)的格式化字符串。
*   雨量保存 CSV 中“雨量(毫米)”字段中保存的值，转换为大十进制。

映射只是准备一个适合我需要的数据结构，没有什么特别复杂的。如果需要，另一个映射可以执行计算来转换数据，也许可以将毫米更改为英寸，以满足另一个系统的要求。

对收集结果的调用会在 mappedData 中保存由地图操作创建的地图列表。这被转换成 JSON，如代码清单 54 所示:映射流的部分输出，它比前面的 JSON 输出更有用。

```groovy
  import static groovy.json.JsonOutput.prettyPrint
  import
  static groovy.json.JsonOutput.toJson
  import
  static java.nio.file.Paths.get as getFile

  @Grab('org.apache.commons:commons-csv:1.2')
  import
  static org.apache.commons.csv.CSVFormat.*

  def
  weatherData
  = RFC4180.withHeader()
      .parse(getFile('../data/weather_data.csv').newReader())
      .getRecords()

  def
  mappedData
  = weatherData.stream()
      .filter { it.Year == '2008' }
      .filter { it.Month == '02' }
      .map {
          [date: "${it.Year}-${it.Month}-${it.Day}",
           rainfall: it.'Rainfall (millimetres)'.toBigDecimal()]
      }.collect()

  print prettyPrint(toJson(mappedData))

```

```groovy
  [

  {

  "date": "2008-02-01",

  "rainfall": 345.4

  },

  {

  "date": "2008-02-02",

  "rainfall": 206.0

  },

```

从前面的例子中继续，我们可以使用 collect 方法来执行一个映射操作作为终止操作。看看代码清单 53:映射一个流，你会看到我已经取出了对映射操作的调用，并调用了带有参数的 collect 方法到 [Collectors.toMap](https://docs.oracle.com/javase/8/docs/api/java/time/Month.html#toMap-java.util.function.Function-java.util.function.Function-) 静态方法。请记住，Groovy 允许我们在调用方法时跳过括号的使用，我将此选项用于对方付费调用的参数，但将括号用于 toMap 调用。这将干净地对参数进行分组，并避免过多的语法。

toMap 方法将每个流项累积成一个映射项。该调用被传递两个闭包——第一个闭包为映射生成密钥；第二个返回地图的值。正如您将在代码清单 56:收集流的部分输出中看到的，我们现在有了一个非常简洁的问题答案。

```groovy
  import static java.nio.file.Paths.get as getFile
  @Grab('org.apache.commons:commons-csv:1.2')
  import
  static org.apache.commons.csv.CSVFormat.*

  import
  static groovy.json.JsonOutput.prettyPrint
  import
  static groovy.json.JsonOutput.toJson
  import
  static java.util.stream.Collectors.toMap

  def
  weatherData
  = RFC4180.withHeader()
          .parse(getFile('../data/weather_data.csv').newReader())
          .getRecords()

  def
  mappedData
  = weatherData.stream()
          .filter { it.Year == '2008' }
          .filter { it.Month == '02' }
          .collect toMap({"${it.Year}-${it.Month}-${it.Day}"},
                         { it.'Rainfall (millimetres)'.toBigDecimal() })

  print prettyPrint(toJson(mappedData))

```

```groovy
  {

  "2008-02-21": 142.1,

  "2008-02-22": 53.5,

  "2008-02-23": 29.2,

  "2008-02-01": 345.4,

  "2008-02-24": 327.5,

```

到目前为止，我已经过滤了一个流，并以基本的方式映射了数据。我没有进行任何计算来总结数据，这就是减少的原因。你可能以前听说过 [MapReduce](http://www.oracle.com/technetwork/java/javase/downloads/jdk8-downloads-2133151.html) 模型——本质上，它由一个过程组成，通过这个过程，数据被过滤、排序和转换(Map 部分)，然后被总结(Reduce 部分)。汇总操作可能会确定平均值(平均值)、最大值、最小值、总和或对数据执行的任何其他计算，以返回感兴趣的值。

流提供了各种终端操作，这些操作执行 MapReduce 的 Reduce 组件。此外，流提供了许多方便的方法，使我们不必编写自己的 Reduce 函数。

除了特定的方法(如 average())之外，收集终端操作还可以通过一个[收集器](http://www.oracle.com/technetwork/articles/java/java8-optional-2175753.html)来执行某种缩减。在前一节中，我使用了 [Collectors](https://hc.apache.org/) 类中提供的 toMap 方法，在编写自己的 Collectors 之前，这是一个很好的检查地方。

让我们准备另一个问题来回答:“2008 年 2 月的平均降雨量是多少？”

代码清单 58:计算平均值创建流，然后以我们在前面例子中看到的相同方式过滤它。一旦布局完成，我们接下来调用 mapToDouble，因为这个方法让我们提取“降雨量(毫米)”字段的值(转换为 Double)。我们这样做是因为 mapToDouble 方法返回了一个专门的流( [DoubleStream](http://commons.apache.org/proper/commons-cli/) )，它为我们提供了一种简单的方法来确定过滤项目的平均值。

因此，当原始流是 CSV 中的记录列表时，我们使用 mapToDouble 根据指定的字段/列分割出一个流。然后，我们可以使用这个流来执行聚合操作——在这种情况下，它是 average()计算。在这一点上，有理由认为我们会有一个单一的值，2008 年 2 月的平均降雨量。然而，我们实际上有一个 OptionalDouble，这需要一些解释。

在编程中， [null](http://twitter.com/dedickinson/) 的概念代表没有值——这意味着 null 本身不是值。Null 会让开发人员感到非常沮丧，因为它要么会创建异常，要么必须进行处理以避免这些异常。在 Java 和 Groovy 世界中，试图调用一个空变量(即它不包含任何值)会导致一个 NullPointerException 和不小的麻烦。

在代码清单 57 中:该死的空！，我写了一些非常愚蠢的代码，设置了一个新的 map 变量，将其赋值为 null，然后尝试访问一个不再存在的元素。这是你永远不应该在野外看到的，但是想象一下这是一个更大的应用程序，其中我的 details 变量被代码的其他部分赋为 null。也许这个任务是一个整理的行为，我不知道。不管怎样，我的代码现在抛出了一个 NullPointerException，并进入了一个令人尖叫的停顿。

```groovy
  def details = [name: 'Fred']

  details = null

  println
  details.name

```

Groovy 通过使用安全导航操作符(？。)，我可以使用 println 细节？。名称以避免异常。Groovy 还将 null 解释为 false，这意味着我可以使用三元运算符:println details？详细信息。名称:“空”。

Java 8 版本包括[可选的](http://commons.apache.org/proper/commons-csv/)类及其兄弟类，包括[双可选的](https://docs.oracle.com/javase/8/docs/api/java/util/OptionalDouble.html)。可选对象可以包含值，也可以不包含值。这是一个方便的抽象，因为它提供了一种包装，提供了另一种避免空指针异常的方法。我们可以通过调用可选的 isPresent 方法来检查它是否包含值。但是，我们可以简单地使用可选的 orElse 方法，并提供一个默认值作为参数。

在代码清单 58:计算平均值中，调用 Average()返回一个 DoubleOptional，因此我们调用它的 orElse 方法来访问该值。如果对 average 的调用碰巧导致 null，那么对 orElse 的调用将导致返回零(0)，并避免 NullPointerException。

```groovy
  import static java.nio.file.Paths.get as getFile
  @Grab('org.apache.commons:commons-csv:1.2')
  import
  static org.apache.commons.csv.CSVFormat.*

  def
  weatherData
  = RFC4180.withHeader()
      .parse(getFile('../data/weather_data.csv').newReader())
      .getRecords()

  println weatherData.stream()
      .filter { it.Year == '2008' }
      .filter { it.Month == '02' }
      .mapToDouble { record ->
          record.'Rainfall (millimetres)'.toDouble()
      }
      .average()
      .orElse(0)

```

如果我问一个问题“2008 年 2 月的总降雨量是多少？”如果你认为可以简单地用一个对 sum()的调用来替换对 average()的调用，那你就可以原谅了，但是，令人恼火的是，这并不完全正确。代码清单 59:计算 sum，说明 Sum()方法不返回可选值，而是给我们一个直接值。查看应用编程接口文档，看看您是否会得到一个值或一个可选的返回值，总是很有用的。

```groovy
  import static java.nio.file.Paths.get as getFile
  @Grab('org.apache.commons:commons-csv:1.2')
  import
  static org.apache.commons.csv.CSVFormat.*

  def
  weatherData
  = RFC4180.withHeader()
      .parse(getFile('../data/weather_data.csv').newReader())
      .getRecords()

  println weatherData.stream()
      .filter { it.Year == '2008' }
      .filter { it.Month == '02' }
      .mapToDouble { record ->
          record.'Rainfall (millimetres)'.toDouble()
      }
      .sum()

```

![](img/00014.jpeg)提示:这些示例都是基于“干净”的数据，并假设 CSV 中的数字字段包含数字。如果你的数据很乱，值得用地图操作来整理一下。

虽然我们可以使用 sum、average、max、min 和 count 等操作来汇总流，但我们也可以在对方付费呼叫中执行 reduce 操作。这将让我们回答“2008 年 2 月的降雨量是多少？”

代码清单 60:汇总数据演示了流如何轻松地为一组值准备汇总统计数据。在这种情况下，我总结了收集操作中的降雨量读数。为了执行聚合，我们将对方付费方法调用传递给[collectors . summaryzingdouble](https://docs.oracle.com/javase/8/docs/api/java/util/stream/Collectors.html#summarizingDouble-java.util.function.ToDoubleFunction-)。后一种方法接受一个闭包，该闭包为流中的每个项返回一个 Double 值。在这种情况下，我们简单地将雨量读数转换为一个双精度值。然后，流的结果输出以 JSON 格式显示在代码清单 61:汇总数据的输出中。

```groovy
  import static groovy.json.JsonOutput.prettyPrint
  import
  static groovy.json.JsonOutput.toJson
  import
  static java.nio.file.Paths.get as getFile
  import
  static java.util.stream.Collectors.summarizingDouble

  @Grab('org.apache.commons:commons-csv:1.2')
  import
  static org.apache.commons.csv.CSVFormat.*

  def
  weatherData
  = RFC4180.withHeader()
          .parse(getFile('../data/weather_data.csv').newReader())
          .getRecords()

  def
  summaryData
  = weatherData.stream()
      .filter { it.Year == '2008' }
      .filter { it.Month == '02' }
      .collect summarizingDouble{it.'Rainfall (millimetres)'.toDouble()}

  print prettyPrint(toJson(summaryData))

```

```groovy
  {

  "sum": 6283.5,

  "average": 216.67241379310346,

  "max": 380.6,

  "min": 29.2,

  "count": 29
  }

```

将数据分成组，特别是对于汇总/聚合操作，通常是有用的。我们可以通过调用 [Collectors.groupingBy](http://mvnrepository.com/#groupingBy-java.util.function.Function-) 方法在对方付费操作中实现这一点。

让我们通过提出“2008 年的月降雨量统计是多少？”这个问题来尝试分组答案看起来类似于代码清单 60:汇总数据，但这只是给了我们一个月的汇总。本质上，该解决方案总结了过滤后的数据，并将过滤器应用于特定的月份和年份组合。为了解决这个问题，我们必须只过滤一年，然后计算每个月的摘要。幸运的是，我们可以用一条小溪做到这一点。

代码清单 63:按月汇总为 2008 年的项目设置流和过滤器。然后，我们使用地图操作为每个项目设置一个地图。这张地图包含两个键:

*   日期—使用我设置的有用的闭包(getDate)构建。
*   雨量-使用大十进制表示读数。

我们现在有一个 2008 年的“数据集”，表示为一系列日期和降雨场。为了回答这个问题，我们需要按月对这些数据进行分组，对于每个月，我们必须准备汇总数据。这是在收集操作中通过调用 Collectors.groupingBy 的形式来执行的，它采用两个参数:

*   作为闭包提供的组的分类器。这很简单——我们只需从日期字段中获取月份部分。
*   用于执行还原的收集器。为此，我们对降雨量数据进行了汇总。

代码清单 62:按月汇总的部分输出提供了代码清单 63:按月汇总显示的部分 JSON。JSON 是一系列地图项目，每个项目代表 2008 年的一个月，并包含一个汇总数据的地图。

```groovy
  {

  "APRIL": {

  "sum": 6389.8,

  "average": 212.99333333333334,

  "max": 393.7,

  "min": 5.3,

  "count": 30

  },

  "SEPTEMBER": {

  "sum": 6498.5,

  "average": 216.61666666666667,

  "max": 395.2,

  "min": 11.1,

  "count": 30

  },

```

```groovy
  import static groovy.json.JsonOutput.prettyPrint
  import
  static groovy.json.JsonOutput.toJson
  import
  static java.nio.file.Paths.get as getFile
  import
  static java.util.stream.Collectors.groupingBy
  import
  static java.util.stream.Collectors.summarizingDouble

  @Grab('org.apache.commons:commons-csv:1.2')
  import
  static org.apache.commons.csv.CSVFormat.*

  import
  java.time.LocalDate

  def
  weatherData
  = RFC4180.withHeader()
          .parse(getFile('../data/weather_data.csv').newReader())
          .getRecords()

  def
  getDate
  = { y, m, d ->
      LocalDate.of(y.toInteger(),
              m.toInteger(),
              d.toInteger())
  }

  def
  monthlyData
  = weatherData.stream()
      .filter { it.Year == '2008' }
      .map {
          [date: getDate(it.Year, it.Month, it.Day),
           rainfall: it.'Rainfall (millimetres)'.toBigDecimal()]
      }.collect groupingBy({ it.date.month },
                           summarizingDouble{it.rainfall.doubleValue()})

  print prettyPrint(toJson(monthlyData))

```

在代码清单 63:按月汇总中，我为日期字段准备了一个 LocalDate 对象，但这并不是严格需要的，因为我可以使用两位数的月值作为日期。我更进一步，这样我得到的数据(和 JSON)就会有一个真正的日期值，可以更容易地被其他代码利用。

groupingBy 方法允许我们以级联方式创建子组。这允许我们将数据分组到我们需要的级别，然后执行聚合。

分组将帮助我们回答“按年分组的日降雨量记录是多少？”这并不是要求我们进行计算，只是一种数据结构，其中每日降雨量记录按月分组，而月本身又按年分组。

代码清单 64:按年分组然后按月分组有点像我们前面的例子，因为 groupingBy 方法是在 collect 操作中调用的。对 group bingby 的第一次调用基于年份(第一个参数)，然后调用另一个 group bingby(第二个参数)。第二个 groupingBy 在分组年份(例如 2006 年)的上下文中工作，因此在另一年中属于同一个月份的数据没有交叉。在第二个分组(月)时，我们调用 toMap 来创建一个地图，方式类似于代码清单 55:收集一个流。

```groovy
  import static groovy.json.JsonOutput.prettyPrint
  import
  static groovy.json.JsonOutput.toJson
  import
  static java.nio.file.Paths.get as getFile
  import
  static java.util.stream.Collectors.groupingBy
  import
  static java.util.stream.Collectors.toMap

  @Grab('org.apache.commons:commons-csv:1.2')
  import
  static org.apache.commons.csv.CSVFormat.*

  import
  java.time.LocalDate

  def
  weatherData
  = RFC4180.withHeader()
          .parse(getFile('../data/weather_data.csv').newReader())
          .getRecords()

  def
  getDate
  = { y, m, d ->
      LocalDate.of(y.toInteger(),
              m.toInteger(),
              d.toInteger())
  }

  def
  monthlyData
  = weatherData.stream()
      .map {
          [date: getDate(it.Year, it.Month, it.Day),
           rainfall: it.'Rainfall (millimetres)'.toBigDecimal()]
      }.collect groupingBy({ it.date.year },
                  groupingBy({ it.date.month },
                      toMap({ it.date.day },{ it.rainfall })))

  print prettyPrint(toJson(monthlyData))

```

如代码清单 65 所示:按年分组然后按月分组的部分输出，得到的 JSON 是一系列包含一系列月对象的年对象，每个月对象包含一个月中每天的降雨量读数图。

```groovy
  {

  "2006": {

  "DECEMBER": {

  "1": 237.2,

  "2": 188.2,

  "3": 359.6,

  "4": 231.1,

  "5": 93.2,

  "6": 337.0,

  "7": 234.2,

  "8": 246.8,

  "9": 184.0,

  "10": 332.6,

```

我们之前问题的目的是重组 CSV 文件中的数据，但更有可能的是我们会问“每年每个月的平均降雨量是多少？”解决方案代码类似于前面的代码，但是我们在子组中调用 averagingDouble 而不是 toMap。

```groovy
  import static groovy.json.JsonOutput.prettyPrint
  import
  static groovy.json.JsonOutput.toJson
  import
  static java.nio.file.Paths.get as getFile
  import
  static java.util.stream.Collectors.averagingDouble
  import
  static java.util.stream.Collectors.groupingBy

  @Grab('org.apache.commons:commons-csv:1.2')
  import
  static org.apache.commons.csv.CSVFormat.*

  def
  weatherData
  = RFC4180.withHeader()
          .parse(getFile('../data/weather_data.csv').newReader())
          .getRecords()

  def
  monthlyAverages
  = weatherData.stream()
      .collect groupingBy({ "$it.Year" },
                  groupingBy({ "$it.Month" },
                      averagingDouble { 
                          it.'Rainfall (millimetres)'.toDouble() 

  }))

  print prettyPrint(toJson(monthlyAverages))

```

在代码清单 66:按年-月平均中，您会看到我跳过了到本地日期的转换，而是使用了 CSV 中的原始年和月值。我更喜欢使用 LocalDate 方法，但我想演示一种替代方法。在内部分组(按月)中，我从 Collectors 类调用 averagingDouble 方法。这接受了一个闭包，我用它为每条记录提供一个双精度值，然后计算组中值的平均值。

这将给我一个有用的年对象输出结构，每个包含它们的组成月份和每个月的平均降雨量。您可以在代码清单 67 中看到输出的第一部分:按年-月平均的部分输出。

```groovy
  {

  "2015": {

  "10": 198.66774193548386,

  "11": 183.61666666666667,

  "12": 240.41935483870967,

  "01": 161.56129032258065,

```

在阅读前面的代码清单时，您可能已经注意到月份顺序不对。如果收件人代码不需要对数据进行排序，这可能不是问题。此外，由于不用担心排序，流操作在处理上有更大的灵活性。管道的不同块可以被发送到其他线程甚至处理器，当它们被返回时并不重要。你可能经常不关心分类/排序，但并不是每个人都这样。

Stream 类中的排序方法提供了一个中间操作，您猜对了，它返回一个由排序元素组成的流。排序的第一个变体使用流元素的自然顺序。如果你有一个像我们之前看到的双流这样的数字流，这是很好的。第二个变量让我们提供自己的比较器，这对于我们的下一个问题来说很方便:“我可以按降序列出 2008 年 2 月的日降雨量吗？”

代码清单 68:排序一个流过滤适当的记录，然后调用排序的。如果我只有一串降雨记录，我可以简单地依靠排序而不用比较器，但是我会失去记录的日期，我希望在我的答案中有这个。这意味着我必须提供我自己的比较器，这是一个接受两个参数的闭包:用于比较的第一个和第二个对象。如您所见，我提供的闭包调用另一个闭包(sortDescending)，它使用宇宙飞船操作符(在操作符中提到)来提供比较。

我没有使用收集操作来终止流，而是使用 forEach 来显示结果。看过 Groovy 的每种方法后，您不会对代码清单 69:流排序的部分输出中的示例输出感到过于惊讶。

```groovy
  import static java.nio.file.Paths.get as getFile
  @Grab('org.apache.commons:commons-csv:1.2')
  import
  static org.apache.commons.csv.CSVFormat.*

  def
  weatherData
  = RFC4180.withHeader()
          .parse(getFile('../data/weather_data.csv').newReader())
          .getRecords()

  def
  sortDescending
  = { n1, n2 ->
      n1.toBigDecimal() <=> n2.toBigDecimal()
  }

  weatherData.stream()
      .filter { it.Year == '2008' }
      .filter { it.Month == '02' }
      .sorted { day1, day2 ->
          sortDescending day2.'Rainfall (millimetres)',
                         day1.'Rainfall (millimetres)' }
      .forEach {
          println "$it.Day: ${it.'Rainfall (millimetres)'}"
      }

```

自然，我们可以使用 forEach 来执行更严肃的操作——也许将数据发送到 web 服务。

```groovy
  14:
  380.6
  13:
  373.0
  06:
  371.5
  08:
  370.8
  07:
  362.5
  01:
  345.4
  24:
  327.5

```

在本章中，我们对天气数据进行了过滤、分组和简化。那些有 SQL 背景的人可能看到了 select、where 和 order by 语句的一些方面，以及聚合函数的一些方面。我倾向于从这个角度来看流——它们给了我一种特定于领域的语言来处理数据系列。

虽然我必须从文件中加载数据，但我使用一个现有的库(Apache Commons CSV)来完成繁重的工作。这类似于 SQL 的 from 语句，但是 CSV 库不太抽象，所以我仍然需要做一点准备。因为 Java 流继续被折叠成通用的，我们可能会看到库进一步简化了这一方面。不过，就目前而言，这已经足够容易了。

在本章的示例中，我将 CSV 读取作为一个工作单元执行，将流操作作为另一个工作单元执行。这意味着我需要一个临时变量(如天气数据或月数据)来保存解析后的 CSV 数据。我的目标是提高可读性和对学习的理解，但是整个过程可以在单个方法调用链中执行，如代码清单 70 所示:CSV 文件和流的单个方法链。最终，您将对可读性更强的内容进行调用，但该链真正捕获了读入数据、过滤、映射和减少(汇总)的过程。

```groovy
  import static java.nio.file.Paths.get as getFile
  @Grab('org.apache.commons:commons-csv:1.2')
  import
  static org.apache.commons.csv.CSVFormat.*
  import
  static java.util.stream.Collectors.summarizingDouble

  def
  displayJson
  = { data ->
      print prettyPrint(toJson(data))
  }

  displayJson RFC4180.withHeader()
          .parse(getFile('../data/weather_data.csv').newReader())
          .getRecords()
          .stream()
          .filter { it.Year == '2008' }
          .filter { it.Month == '02' }
          .collect(summarizingDouble { it.'Rainfall
  (millimetres)'.toDouble() })

```

Java streams 的内容远不止于此，以下资源将有助于您的理解:

*   用 Java SE 8 Streams 处理数据是由拉乌尔-加布里埃尔·乌尔马从 [Java 杂志](XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)上撰写的两篇文章，提供了一个很好的教程:

*   [第 1 部分](https://bintray.com/bintray/jcenter)来自 2014 年 3/4 月刊。
*   【2014 年 5/6 月刊第 2 部分。

*   [流类](http://dl.bintray.com/groovy/Distributions/groovy-2.4.5-installer.exe)和 [java.util.stream 包](https://docs.oracle.com/javase/8/docs/api/java/time/package-summary.html)的 Java 8 应用编程接口文档记录得非常好。
*   [厌倦了空指针异常？考虑使用 Java SE 8 的可选功能！](https://docs.oracle.com/javase/8/docs/api/java/math/BigDecimal.html)由拉乌尔-加布里埃尔乌尔马也是一个值得的资源。